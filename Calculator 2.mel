proc string Calculate (string $problemText)
{
    string $sSets[] = stringToStringArray($problemText, "-");

    float $answer;

    int $sSize = size($sSets);
    for ($s = 0; $s < $sSize; $s++)
    {
        string $aSets[] = stringToStringArray($sSets[$s], "+");

        float $aResult = 0;

        int $aSize = size($aSets);
        for ($a = 0; $a < $aSize; $a++)
        {
            string $dSets[] = stringToStringArray($aSets[$a], "/");

            float $dResult;

            int $dSize = size($dSets);
            for ($d = 0; $d < $dSize; $d++)
            {
                string $mSets[] = stringToStringArray($dSets[$d], "*");

                float $mResult = 1;

                int $mSize = size($mSets);
                for ($m = 0; $m < $mSize; $m++)
                {
                    $mResult *= (float)$mSets[$m];
                }

                if ($d == 0)
                {
                    $dResult = $mResult;
                }
                else
                {
                    $dResult /= $mResult;
                }
            }

            if ($a == 0)
            {
                $aResult = $dResult;
            }
            else
            {
                $aResult += $dResult;
            }
        }

        if ($s == 0)
        {
            $answer = $aResult;
        }
        else
        {
            $answer -= $aResult;
        }
    }


    $answerText = $problemText + "=" + $answer;

    return $answerText;
}

string $myWindow = "CalculatorWindow";

if (`window -exists $myWindow`)
{
    deleteUI $myWindow;
}

window -title "Calculator" -widthHeight 300 200 $myWindow;
    // columnLayout;
    // columnLayout -adjustableColumn true;
    string $myColumn = `columnLayout -parent $myWindow -adjustableColumn 1`;
    optionVar -stringValue "problemField" `textField -parent $myColumn`;
    button -label "Calculate" -command "ButtonCommand";
    optionVar -stringValue "resultField" `textField -parent $myColumn -editable 0`;
showWindow $myWindow;

global proc ButtonCommand ()
{
    string $problemField = `optionVar -q "problemField"`;
    string $problem = `textField -q -text $problemField`;
    string $result = Calculate($problem);

    string $resultField = `optionVar -q "resultField"`;

    textField -e -text $result $resultField;
}